'===================================
'Macro Name: Trade APS Split Generator
'Peter Riad 2025
'Version: v3.4
'===================================

Private Sub Workbook_Open()
    AddRunMacroButton
    AddClearDataButton
    AddEmailButton
End Sub

Function SheetExists(sheetName As String) As Boolean
    On Error Resume Next
    SheetExists = Not Sheets(sheetName) Is Nothing
    On Error GoTo 0
End Function

Sub SplitDataByColumnJ()
    Dim wsSource As Worksheet: Set wsSource = ThisWorkbook.Sheets("MainData")
    Dim wsLookup As Worksheet: Set wsLookup = ThisWorkbook.Sheets("Client Data")
    Dim lastRow As Long: lastRow = wsSource.Cells(wsSource.Rows.Count, "K").End(xlUp).Row
    Dim headers As Range: Set headers = wsSource.Range("C1:M1")

    Dim groups As Variant, sheetNames As Variant, timeNotes As Variant
    groups = Array( _
        Array("33", "49", "72", "69", "20", "22", "24", "18", "7Z", "11", "17"), _
        Array("30", "32", "8R"), _
        Array("8G", "12"), _
        Array("27", "52"), _
        Array("28", "25", "7R", "10", "13", "SG", "DA", "PA"), _
        Array("19", "05", "14", "06", "04", "07", "5", "6", "4", "7"), _
        Array("7B", "01", "9C", "02", "16", "15", "1", "2") _
    )
    sheetNames = Array( _
        "33_49_72_69_20_22_24_18_7Z_11", "30_32_8R", "8G_12", "27_52", _
        "28_25_7R_10_13_SG_DA_PA", "19_05_14_06_04_07", "7B_01_9C_02_16_15" _
    )
    timeNotes = Array( _
        "12 PM – 1 PM", "1:30 PM – 2:30 PM", "3 PM – 4 PM", "4:30 PM – 5:30 PM", _
        "5 PM – 6 PM", "6:30 PM – 7 PM", "7 PM – 8 PM" _
    )

    Dim lookupData As Variant
    lookupData = wsLookup.Range("A2:J" & wsLookup.Cells(wsLookup.Rows.Count, "C").End(xlUp).Row).Value

    Dim i As Long, j As Long, wsNew As Worksheet, val As String
    Dim wsFirst As Worksheet
    Application.ScreenUpdating = False

    For i = 0 To UBound(groups)
        On Error Resume Next
        Application.DisplayAlerts = False
        Sheets(sheetNames(i)).Delete
        Application.DisplayAlerts = True
        On Error GoTo 0

        Set wsNew = Sheets.Add(After:=Sheets(Sheets.Count))
        wsNew.Name = sheetNames(i)
        If i = 0 Then Set wsFirst = wsNew

        headers.Copy Destination:=wsNew.Range("A1")
        wsNew.Range("A1:K1").Font.Bold = True
        wsNew.Cells(1, 12).Resize(1, 9).Value = Array("Email?", "Match Office", "Match Acct", "Status", "Client Name", "Client Type", "Behavior", "Notes", "Notes 2")
        wsNew.Cells(1, 21).Value = "Behavior Match"
        wsNew.Range("V1").Value = "Time Slot: " & timeNotes(i)
        wsNew.Range("V1").Font.Bold = True
        wsNew.Range("V1").Interior.Color = RGB(255, 255, 0)

        Dim destRow As Long: destRow = 2

        For j = 2 To lastRow
            val = Trim(wsSource.Cells(j, 10).Value)
            If Not IsError(Application.Match(val, groups(i), 0)) Then
                wsNew.Range("A" & destRow & ":K" & destRow).Value = wsSource.Range("C" & j & ":M" & j).Value
                wsNew.Cells(destRow, 12).Value = "" ' Email? starts blank

                Dim rowD As String: rowD = UCase(Trim(wsSource.Cells(j, 4).Value))
                Dim rowE As String: rowE = UCase(Trim(wsSource.Cells(j, 5).Value))
                Dim matched As Boolean: matched = False

                Dim l As Long
                For l = 1 To UBound(lookupData)
                    Dim lookupC As String: lookupC = UCase(lookupData(l, 3))
                    Dim lookupD As String: lookupD = UCase(lookupData(l, 4))
                    If lookupC <> "" And lookupC <> "-" Then
                        If rowD = lookupC And rowE = lookupD Then matched = True
                    ElseIf rowE = lookupD Then
                        matched = True
                    End If

                    If matched Then
                        wsNew.Cells(destRow, 13).Resize(1, 8).Value = Array(lookupC, lookupD, "Matched", lookupData(l, 1), lookupData(l, 7), lookupData(l, 8), lookupData(l, 9), lookupData(l, 10))
                        wsNew.Cells(destRow, 21).Value = "Matched"
                        Exit For
                    End If
                Next l

                If Not matched Then
                    wsNew.Cells(destRow, 15).Value = "Unmatched"
                    wsNew.Cells(destRow, 21).Value = "Unmatched"
                    wsNew.Cells(destRow, 15).Interior.Color = RGB(255, 192, 0)
                    wsNew.Cells(destRow, 21).Interior.Color = RGB(255, 192, 0)
                End If

                destRow = destRow + 1
            End If
        Next j

        ' Date highlight
        Dim todayVal As String: todayVal = Format(Date, "yyyymmdd")
        For j = 2 To destRow - 1
            If Trim(wsNew.Cells(j, 4).Value) <> todayVal Then
                wsNew.Cells(j, 4).Interior.Color = RGB(255, 0, 0)
            End If
        Next j

        ' Tab Color by row count
        Dim dataCount As Long: dataCount = destRow - 2
        Select Case dataCount
            Case 0: wsNew.Tab.Color = RGB(0, 176, 80)
            Case 1 To 25: wsNew.Tab.Color = RGB(255, 255, 0)
            Case 26 To 40: wsNew.Tab.Color = RGB(255, 192, 0)
            Case Is > 40: wsNew.Tab.Color = RGB(255, 0, 0)
        End Select

        ' Highlight if in EOD Chased
        If SheetExists("EOD Chased") Then
            Dim wsEOD As Worksheet: Set wsEOD = Sheets("EOD Chased")
            Dim eodRow As Long: eodRow = wsEOD.Cells(wsEOD.Rows.Count, "A").End(xlUp).Row
            If eodRow > 1 Then
                Dim eodData As Variant: eodData = wsEOD.Range("A2:K" & eodRow).Value
                For j = 2 To destRow - 1
                    Dim currentRowData As Variant
                    currentRowData = Application.Transpose(Application.Transpose(wsNew.Range("A" & j & ":K" & j).Value))
                    Dim currentRowStr As String: currentRowStr = Join(currentRowData, "|")
                    Dim r As Variant
                    For Each r In eodData
                        If currentRowStr = Join(Application.Transpose(Application.Transpose(r)), "|") Then
                            wsNew.Rows(j).Interior.Color = RGB(200, 200, 200)
                            Exit For
                        End If
                    Next r
                Next j
            End If
        End If

        ' Sorting
        With wsNew.Sort
            .SortFields.Clear
            .SortFields.Add Key:=wsNew.Range("J2:J" & destRow - 1)
            .SortFields.Add Key:=wsNew.Range("K2:K" & destRow - 1)
            .SortFields.Add Key:=wsNew.Range("I2:I" & destRow - 1)
            .SortFields.Add Key:=wsNew.Range("E2:E" & destRow - 1)
            .SetRange wsNew.Range("A1:V" & destRow - 1)
            .Header = xlYes
            .Apply
        End With

        wsNew.Columns("A:W").AutoFit
        wsNew.Rows("1:1").AutoFilter
    Next i

    wsFirst.Activate
    Application.ScreenUpdating = True
    MsgBox "Data split, validated, and highlighted!"
End Sub

Sub ClearMainDataSheet()
    Dim ws As Worksheet
    For Each ws In ThisWorkbook.Sheets
        If ws.Name <> "MainData" And ws.Name <> "Client Data" And ws.Name <> "EOD Chased" Then
            Application.DisplayAlerts = False
            ws.Delete
            Application.DisplayAlerts = True
        ElseIf ws.Name = "MainData" Then
            ws.Rows("2:" & ws.Cells(ws.Rows.Count, "A").End(xlUp).Row).ClearContents
        End If
    Next ws
    MsgBox "MainData cleared and split sheets removed!"
End Sub

Sub AddRunMacroButton()
    Dim ws As Worksheet: Set ws = Sheets("MainData")
    On Error Resume Next: ws.Shapes("btnSplitData").Delete: On Error GoTo 0
    With ws.Buttons.Add(ws.Range("R2").Left, ws.Range("R2").Top, ws.Range("R2:T2").Width, ws.Range("R2:T2").Height + 5)
        .Caption = "Split Data"
        .Name = "btnSplitData"
        .OnAction = "SplitDataByColumnJ"
    End With
End Sub

Sub AddClearDataButton()
    Dim ws As Worksheet: Set ws = Sheets("MainData")
    On Error Resume Next: ws.Shapes("btnClearMainData").Delete: On Error GoTo 0
    With ws.Buttons.Add(ws.Range("R4").Left, ws.Range("R4").Top, ws.Range("R4:T4").Width, ws.Range("R4:T4").Height + 5)
        .Caption = "Clear Main Data"
        .Name = "btnClearMainData"
        .OnAction = "ClearMainDataSheet"
    End With
End Sub

Sub AddEmailButton()
    Dim ws As Worksheet: Set ws = Sheets("MainData")
    On Error Resume Next: ws.Shapes("btnEmailRemaining").Delete: On Error GoTo 0
    With ws.Buttons.Add(ws.Range("R6").Left, ws.Range("R6").Top, ws.Range("R6:T6").Width, ws.Range("R6:T6").Height + 5)
        .Caption = "Email Remaining by Client"
        .Name = "btnEmailRemaining"
        .OnAction = "EmailRemainingByClient"
    End With
End Sub

Sub EmailRemainingByClient()
    Dim ws As Worksheet, clientRows As Object: Set clientRows = CreateObject("Scripting.Dictionary")

    For Each ws In ThisWorkbook.Sheets
        If ws.Name <> "MainData" And ws.Name <> "Client Data" And ws.Name <> "EOD Chased" Then
            Dim lastRow As Long: lastRow = ws.Cells(ws.Rows.Count, "A").End(xlUp).Row
            Dim i As Long
            For i = 2 To lastRow
                If LCase(Trim(ws.Cells(i, 12).Value)) = "yes" Or LCase(Trim(ws.Cells(i, 12).Value)) = "y" Then
                    Dim clientKey As String: clientKey = ws.Cells(i, 16).Value
                    If clientKey <> "" Then
                        If Not clientRows.exists(clientKey) Then Set clientRows(clientKey) = New Collection
                        clientRows(clientKey).Add ws.Rows(i)
                    End If
                End If
            Next i
        End If
    Next ws

    Dim olApp As Object: Set olApp = CreateObject("Outlook.Application")
    Dim outMail As Object, client As Variant

    For Each client In clientRows.keys
        Set outMail = olApp.CreateItem(0)
        Dim outMsg As String: outMsg = "<b>Missing Allocations:</b><br><br><table border='1' cellpadding='3'><tr>"
        For i = 1 To 11: outMsg = outMsg & "<th>" & clientRows(client)(1).Parent.Cells(1, i).Text & "</th>": Next i
        outMsg = outMsg & "</tr>"

        Dim rw As Range
        For Each rw In clientRows(client)
            outMsg = outMsg & "<tr>"
            For i = 1 To 11: outMsg = outMsg & "<td>" & rw.Cells(1, i).Text & "</td>": Next i
            outMsg = outMsg & "</tr>"
        Next rw
        outMsg = outMsg & "</table><br><br>Thank you,<br>"

        With outMail
            .To = "" ' Fill manually
            .Subject = client & " | Missing Allocations | Barclays | " & Format(Date, "dd-mmm-yyyy")
            .HTMLBody = outMsg
            .Display
        End With
    Next client

    MsgBox "Emails generated for all clients marked 'Yes'."
End Sub
