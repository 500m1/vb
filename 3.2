'===================================
'Macro Name: Trade APS Split Generator
'Author: Peter Riad
'Version: v3.2 (patched)
'===================================

Private Sub Workbook_Open()
    AddRunMacroButton
    AddClearDataButton
    AddEmailButton
End Sub

'========================
' SPLIT DATA
'========================
Sub SplitDataByColumnJ()

    Dim wsSource As Worksheet: Set wsSource = Sheets("MainData")
    Dim wsLookup As Worksheet: Set wsLookup = Sheets("Client Data")

    Dim lastRow As Long
    lastRow = wsSource.Cells(wsSource.Rows.Count, "K").End(xlUp).Row

    Dim headers As Range
    Set headers = wsSource.Range("C1:M1")

    Dim groups, sheetNames, timeNotes
    groups = Array( _
        Array("33", "49", "72", "69", "20", "22", "24", "18", "7Z", "11", "17"), _
        Array("30", "32", "8R"), _
        Array("8G", "12"), _
        Array("27", "52"), _
        Array("28", "25", "7R", "10", "13", "SG", "DA", "PA"), _
        Array("19", "05", "14", "06", "04", "07", "5", "6", "4", "7"), _
        Array("7B", "01", "9C", "02", "16", "15", "1", "2") _
    )

    sheetNames = Array( _
        "33_49_72_69_20_22_24_18_7Z_11", _
        "30_32_8R", _
        "8G_12", _
        "27_52", _
        "28_25_7R_10_13_SG_DA_PA", _
        "19_05_14_06_04_07", _
        "7B_01_9C_02_16_15" _
    )

    timeNotes = Array( _
        "12 PM – 1 PM", _
        "1:30 PM – 2:30 PM", _
        "3 PM – 4 PM", _
        "4:30 PM – 5:30 PM", _
        "5 PM – 6 PM", _
        "6:30 PM – 7 PM", _
        "7 PM – 8 PM" _
    )

    Dim lookupData As Variant
    lookupData = wsLookup.Range("A2:J" & wsLookup.Cells(wsLookup.Rows.Count, "C").End(xlUp).Row).Value

    Application.ScreenUpdating = False

    Dim i As Long, j As Long
    Dim wsNew As Worksheet, wsFirst As Worksheet

    For i = 0 To UBound(groups)

        On Error Resume Next
        Application.DisplayAlerts = False
        Sheets(sheetNames(i)).Delete
        Application.DisplayAlerts = True
        On Error GoTo 0

        Set wsNew = Sheets.Add(After:=Sheets(Sheets.Count))
        wsNew.Name = sheetNames(i)
        If i = 0 Then Set wsFirst = wsNew

        headers.Copy wsNew.Range("A1")
        wsNew.Range("A1:K1").Font.Bold = True

        wsNew.Cells(1, 12).Value = "Email?"
        wsNew.Cells(1, 13).Resize(1, 9).Value = _
            Array("Match Office", "Match Acct", "Status", "Client Name", "Client Type", "Behavior", "Notes", "Notes 2", "Behavior Match")

        With wsNew.Range("V1")
            .Value = "Time Slot: " & timeNotes(i)
            .Font.Bold = True
            .Interior.Color = RGB(255, 255, 0)
        End With

        Dim destRow As Long: destRow = 2

        For j = 2 To lastRow

            If Not IsError(Application.Match(wsSource.Cells(j, 10).Value, groups(i), 0)) Then

                wsNew.Range("A" & destRow & ":K" & destRow).Value = wsSource.Range("C" & j & ":M" & j).Value

                Dim rowD As String: rowD = UCase(Trim(wsSource.Cells(j, 4).Value))
                Dim rowE As String: rowE = UCase(Trim(wsSource.Cells(j, 5).Value))

                Dim matched As Boolean: matched = False
                Dim l As Long

                For l = 1 To UBound(lookupData)

                    Dim lookupC As String: lookupC = UCase(Trim(lookupData(l, 3)))
                    Dim lookupD As String: lookupD = UCase(Trim(lookupData(l, 4)))

                    If lookupC <> "" And lookupC <> "-" Then
                        matched = (rowD = lookupC And rowE = lookupD)
                    Else
                        matched = (rowE = lookupD)
                    End If

                    If matched Then
                        wsNew.Cells(destRow, 13).Resize(1, 8).Value = _
                            Array(lookupC, lookupD, "Matched", lookupData(l, 1), lookupData(l, 7), lookupData(l, 8), lookupData(l, 9), lookupData(l, 10))
                        wsNew.Cells(destRow, 21).Value = "Matched"
                        Exit For
                    End If
                Next l

                If Not matched Then
                    wsNew.Cells(destRow, 15).Value = "Unmatched"
                    wsNew.Cells(destRow, 21).Value = "Unmatched"
                    wsNew.Cells(destRow, 15).Interior.Color = RGB(255, 192, 0)
                    wsNew.Cells(destRow, 21).Interior.Color = RGB(255, 192, 0)
                End If

                destRow = destRow + 1
            End If
        Next j

        ' Date validation
        Dim todayVal As String: todayVal = Format(Date, "yyyymmdd")
        For j = 2 To destRow - 1
            If Trim(wsNew.Cells(j, 4).Value) <> todayVal Then
                wsNew.Cells(j, 4).Interior.Color = RGB(255, 0, 0)
            End If
        Next j

        ' Tab coloring
        Dim cnt As Long: cnt = destRow - 2
        Select Case cnt
            Case 0: wsNew.Tab.Color = RGB(0, 176, 80)
            Case 1 To 25: wsNew.Tab.Color = RGB(255, 255, 0)
            Case 26 To 40: wsNew.Tab.Color = RGB(255, 192, 0)
            Case Else: wsNew.Tab.Color = RGB(255, 0, 0)
        End Select

        ' EOD Chased highlighting
        If SheetExists("EOD Chased") Then
            Dim wsEOD As Worksheet: Set wsEOD = Sheets("EOD Chased")
            Dim eodData As Variant
            Dim eodLast As Long: eodLast = wsEOD.Cells(wsEOD.Rows.Count, "A").End(xlUp).Row
            If eodLast >= 2 Then
                eodData = wsEOD.Range("A2:K" & eodLast).Value

                Dim r As Long, e As Long
                For r = 2 To destRow - 1
                    For e = 1 To UBound(eodData)
                        If LCase(Join(Application.Transpose(Application.Transpose(wsNew.Range("A" & r & ":K" & r).Value)), "|")) = _
                           LCase(Join(Application.Transpose(Application.Transpose(Application.Index(eodData, e, 0))), "|")) Then
                            wsNew.Rows(r).Interior.Color = RGB(200, 200, 200)
                            Exit For
                        End If
                    Next e
                Next r
            End If
        End If

        wsNew.Columns("A:W").AutoFit
        wsNew.Rows(1).AutoFilter
    Next i

    wsFirst.Activate
    Application.ScreenUpdating = True
    MsgBox "Split complete."

End Sub

'========================
' EMAIL REMAINING
'========================
Sub EmailRemainingByClient()

    Dim ws As Worksheet
    Dim dict As Object: Set dict = CreateObject("Scripting.Dictionary")

    For Each ws In Sheets
        If ws.Name <> "MainData" And ws.Name <> "Client Data" And ws.Name <> "EOD Chased" Then
            Dim lr As Long: lr = ws.Cells(ws.Rows.Count, "A").End(xlUp).Row
            Dim i As Long
            For i = 2 To lr
                If LCase(Trim(ws.Cells(i, 12).Value)) = "yes" Or LCase(Trim(ws.Cells(i, 12).Value)) = "y" Then
                    Dim key As String: key = ws.Cells(i, 16).Value
                    If key <> "" Then
                        If Not dict.exists(key) Then Set dict(key) = New Collection
                        dict(key).Add ws.Rows(i)
                    End If
                End If
            Next i
        End If
    Next ws

    Dim olApp As Object: Set olApp = CreateObject("Outlook.Application")
    Dim client, mail, rw As Range

    For Each client In dict.keys
        Set mail = olApp.CreateItem(0)
        Dim body As String: body = "<table border='1'><tr>"
        Dim c As Long

        For c = 1 To 11
            body = body & "<th>" & dict(client)(1).Parent.Cells(1, c).Text & "</th>"
        Next c
        body = body & "</tr>"

        For Each rw In dict(client)
            body = body & "<tr>"
            For c = 1 To 11
                body = body & "<td>" & rw.Cells(1, c).Text & "</td>"
            Next c
            body = body & "</tr>"
        Next rw

        body = body & "</table><br><br>Thank you,"

        With mail
            .Subject = client & " | Missing Allocations | Barclays | " & Format(Date, "dd-mmm-yyyy")
            .HTMLBody = body
            .Display
        End With
    Next client

End Sub

'========================
' CLEAR DATA
'========================
Sub ClearMainDataSheet()
    Dim ws As Worksheet
    For Each ws In Sheets
        If ws.Name <> "MainData" And ws.Name <> "Client Data" And ws.Name <> "EOD Chased" Then
            Application.DisplayAlerts = False
            ws.Delete
            Application.DisplayAlerts = True
        ElseIf ws.Name = "MainData" Then
            ws.Rows("2:" & ws.Cells(ws.Rows.Count, "A").End(xlUp).Row).ClearContents
        End If
    Next ws
End Sub

'========================
' BUTTONS (SAFE)
'========================
Sub AddRunMacroButton()
    AddButtonSafe "btnSplitData", "Split Data", "SplitDataByColumnJ", "R2:T2"
End Sub

Sub AddClearDataButton()
    AddButtonSafe "btnClearMainData", "Clear Main Data", "ClearMainDataSheet", "R4:T4"
End Sub

Sub AddEmailButton()
    AddButtonSafe "btnEmailRemaining", "Email Remaining by Client", "EmailRemainingByClient", "R6:T6"
End Sub

Sub AddButtonSafe(btnName As String, caption As String, macroName As String, rng As String)
    Dim ws As Worksheet: Set ws = Sheets("MainData")
    On Error Resume Next: ws.Shapes(btnName).Delete: On Error GoTo 0
    With ws.Buttons.Add(ws.Range(rng).Left, ws.Range(rng).Top, ws.Range(rng).Width, ws.Range(rng).Height + 5)
        .Name = btnName
        .Caption = caption
        .OnAction = macroName
    End With
End Sub

Function SheetExists(n As String) As Boolean
    On Error Resume Next
    SheetExists = Not Sheets(n) Is Nothing
    On Error GoTo 0
End Function
